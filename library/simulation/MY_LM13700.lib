*//////////////////////////////////////////////////////////////////////
* (C) National Semiconductor, Inc.
* Models developed and under copyright by:
* National Semiconductor, Inc.  

*/////////////////////////////////////////////////////////////////////
* Legal Notice: This material is intended for free software support.
* The file may be copied, and distributed; however, reselling the 
*  material is illegal

*////////////////////////////////////////////////////////////////////
* For ordering or technical information on these models, contact:
* National Semiconductor's Customer Response Center
*                 7:00 A.M.--7:00 P.M.  U.S. Central Time
*                                (800) 272-9959
* For Applications support, contact the Internet address:
*  amps-apps@galaxy.nsc.com

* \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\            
* LM13700 Dual Operational Transconductance Amplifier                 
* \\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\                 
*
*                         Amplifier Bias Input
*                         |   Diode Bias
*                         |   | Positive Input
*                         |   |   |   Negative Input
*                         |   |   |   |   Output
*                         |   |   |   |   |   Negative power supply
*                         |   |   |   |   |   |   Buffer Input
*                         |   |   |   |   |   |   |   Buffer Output
*                         |   |   |   |   |   |   |   |   Positive power supply
*                         |   |   |   |   |   |   |   |   |
.SUBCKT MY_LM13700_MODEL  IAB LIN INP INN OUT VEE BIN BUF VCC
	QN1 IAB VN2B VEE npnv 3
	QN2 VN2B VN2B VEE npnv 3
	QN3 VN3C IAB VN2B npnv 3
	QN4 VP3B INN VN3C npnv 3
	QN5 VP6B INP VN3C npnv 3
	QN6 LIN LIN INN npnv 3
	QN7 LIN LIN INP npnv 3
	QN8 VN10B VN9B VEE npnv 3
	QN9 VN9B VN9B VEE npnv 3
	QN10 OUT VN10B VN9B npnv 3
	QN11 VCC BIN VN12B npnv 15
	QN12 VN12B VN12B BUF npnv 3
	QN13 VCC VN12B BUF npnv 150
	QP1 VP3B VP2B VCC pnpl 3
	QP2 VP2B VP2B VCC pnpl 3
	QP3 VN10B VP3B VP2B pnpl 3
	QP4 VP6B VP5B VCC pnpl 3
	QP5 VP5B VP5B VCC pnpl 3
	QP6 OUT VP6B VP5B pnpl 3

	.MODEL npnv NPN(
		*==========================================================
		+IS=1.1E-18 NF=1.005 BF=220 VAF=130 IKF=5e-03
		+ISE=9.15E-15 NE=2
		*==========================================================
		+CJE=2E-12 CJC=2E-13 CJS=3E-12 TF=.6E-9 )

	.MODEL pnpl PNP(
		*==========================================================
		+IS=1.1E-18 NF=1.8 BF=5 VAF=170 IKF=6e-03
		+ISE=3E-15 NE=3
		*==========================================================
		+CJE=4E-12 CJC=6E-13 CJS=5E-12 TF=70E-9 )

.ends




* LM13700 Dual Operational Transconductance Amplifier
*
* SPICE (Simulation Program with Integrated Circuit Emphasis)
* SUBCIRCUIT
*
* Amplifier Bias Input
* | Diode Bias
* | | Positive Input
* | | | Negative Input
* | | | | Output
* | | | | | V-
* | | | | | | Buffer Input
* | | | | | | | Buffer Output
* | | | | | | | | V+
* | | | | | | | | |
.SUBCKT LM13700_U 1 2 3 4 5 6 7 8 11
*
C1 6 4 4.8P
C2 3 6 4.8P
* Output capacitor
C3 5 6 6.26P
D1 2 4 DX
D2 2 3 DX
D3 11 21 DX
D4 21 22 DX
D5 1 26 DX
D6 26 27 DX
D7 5 29 DX
D8 28 5 DX
D10 31 25 DX
* Clamp for -CMR
D11 28 25 DX
* Ios source
F1 4 3 POLY(1) V6 1E-10 5.129E-2 -1.189E4 1.123E9
F2 11 5 V2 1.022
F3 25 6 V3 1.0
F4 5 6 V1 1.022
* Output impedance
F5 5 0 POLY(2) V3 V7 0 0 0 0 1
G1 0 33 5 0 .55E-3
I1 11 6 300U
Q1 24 32 31 QX1
Q2 23 3 31 QX2
Q3 11 7 30 QZ
Q4 11 30 8 QY
V1 22 24 0V
V2 22 23 0V
V3 27 6 0V
V4 11 29 1.4
V5 28 6 1.2
V6 4 32 0V
V7 33 0 0V
.MODEL QX1 NPN (IS=5E-16 BF=200 NE=1.15 ISE=.63E-16 IKF=1E-2)
.MODEL QX2 NPN (IS=5.125E-16 BF=200 NE=1.15 ISE=.63E-16 IKF=1E-2)
.MODEL QY NPN (IS=6E-15 BF=50)
.MODEL QZ NPN (IS=5E-16 BF=266)
.MODEL DX D (IS=5E-16)
.ENDS



* https://github.com/deanm1278/LM13700-spice-model/blob/master/lm13700.sub
.SUBCKT DEANM_HALF13700    IAB LIN INP INN OUT VSS BIN BUF VCC
QN1     IAB   VN2B   VSS   DEANM_npnv     3
QN2     VN2B  VN2B   VSS   DEANM_npnv     3
QN3     VN3C  IAB    VN2B  DEANM_npnv     3
QN4     VP3B  INN    VN3C  DEANM_npnv     3
QN5     VP6B  INP    VN3C  DEANM_npnv     3
QN6     LIN   LIN    INN   DEANM_npnv     3
QN7     LIN   LIN    INP   DEANM_npnv     3
QN8     VN10B VN9B   VSS   DEANM_npnv     3
QN9     VN9B  VN9B   VSS   DEANM_npnv     3
QN10    OUT   VN10B  VN9B  DEANM_npnv     3
QN11    VCC   BIN    VN12B DEANM_npnv     15
QN12    VN12B VN12B  BUF   DEANM_npnv     3
QN13    VCC   VN12B  BUF   DEANM_npnv     150
QP1     VP3B  VP2B   VCC   DEANM_pnpl     3
QP2     VP2B  VP2B   VCC   DEANM_pnpl     3
QP3     VN10B VP3B   VP2B  DEANM_pnpl     3
QP4     VP6B  VP5B   VCC   DEANM_pnpl     3
QP5     VP5B  VP5B   VCC   DEANM_pnpl     3
QP6     OUT   VP6B   VP5B  DEANM_pnpl     3

.MODEL  DEANM_npnv      NPN(
+IS=1.1E-18     NF=1.005   BF=220        VAF=130      IKF=5e-03 
+ISE=9.15E-15   NE=2 
+CJE=2E-12 CJC=2E-13 CJS=3E-12 TF=.6E-9 )

.MODEL  DEANM_pnpl     PNP(
+IS=1.1E-18     NF=1.8   BF=5        VAF=170      IKF=6e-03 
+ISE=3E-15      NE=3 
+CJE=4E-12 CJC=6E-13 CJS=5E-12 TF=70E-9 )

.ENDS

.SUBCKT CHOSEN_LM13700 IAB LIN INP INN OUT VSS BIN BUF VCC
x1 IAB LIN INP INN OUT VSS BIN BUF VCC MY_LM13700_MODEL
*x1 IAB LIN INP INN OUT VSS BIN BUF VCC LM13700_U
*x1 IAB LIN INP INN OUT VSS BIN BUF VCC DEANM_HALF13700
.ENDS

.SUBCKT MY_LM13700 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16
x1 1  2  3  4  5  6 7  8 11 CHOSEN_LM13700
x2 16 15 14 13 12 6 10 9 11 CHOSEN_LM13700
.ENDS

